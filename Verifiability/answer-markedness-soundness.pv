type id.
type skey.
type role.

free C: role.
free E: role.

free tmpC: channel.

free ch: channel [private].
free mix: channel [private].
free bb: channel.
free chan: channel.

event bad().
event accepted (id, bitstring, bitstring).
event marked (id, bitstring, bitstring).
event ok(id, bitstring, bitstring).
query pkey: id, quest: bitstring, answ: bitstring; event(ok(pkey, quest, answ)) ==> event(marked(pkey,quest,answ)).

fun pk(skey): id.
fun hash(bitstring): bitstring.

fun encrypt(id, bitstring): bitstring.
fun decrypt(skey, bitstring): bitstring
    reduc
        forall k: skey, m: bitstring; decrypt(k, encrypt(pk(k), m)) = m.

fun sign(skey, bitstring) : bitstring.

reduc forall k: skey, m: bitstring; getmess(sign(k, m)) = m.
fun checksign(id, bitstring) : bitstring
    reduc
        forall k: skey, m: bitstring; checksign(pk(k), sign(k, m)) = m.

(*-----------------Exam Authority-----------------*)
let EA (skEA: skey, idABCA: id, idQC: id) =

    new exam: id;
    out(bb, exam);
    (* Request questions *)
    out(ch, exam);
    in(ch, (Q:bitstring, =exam));
    let (qi: bitstring, sqi: bitstring) = Q in
    if (qi, exam) = checksign(idQC, sqi) then

    (* Examiner registration *)
    in(mix, (CrE: bitstring, pseudoE : id, =exam));
    let (credE: bitstring, scredE: bitstring) = CrE in
    if (credE, E) = checksign(idABCA, scredE) then
        out(mix, (exam, true));

    (* Candidate registration *)
    in(mix, (CrC: bitstring, pseudoC : id, =exam));
    let (credC: bitstring, scredC: bitstring) = CrC in
    if (credC, C) = checksign(idABCA, scredC) then
        out(mix, (exam, true));

    (* Create tests *)
    let eQ = encrypt(pseudoC, Q) in
    let test = (eQ, pseudoC, pseudoE) in
    let sTest = sign(skEA, test) in

    (* Testing *)
    out(mix, (test, sTest));
    in(mix, (S: bitstring, sS: bitstring, =pseudoC, =CrC, =exam));
    let (=credC, =scredC) = CrC in
    if (credC, C) = checksign(idABCA, scredC) then
    if S = checksign(pseudoC, sS) then
    let (=qi, si: bitstring) = S in
    new alpha: bitstring;
    let rC = (S, sS, pseudoC, CrC, exam, alpha) in
    let srC = sign(skEA, rC) in
    out(mix, (rC, srC));

    (* Marking *)
    let sSEA = sign(skEA, (qi, si)) in
    out(bb, encrypt(pseudoE, (qi, si, sSEA, exam)));

    in(mix, (M: bitstring, sM: bitstring, =pseudoE, =CrE, =exam));
    let (=credE, =scredE) = CrE in
    if (credE, E) = checksign(idABCA, scredE) then 
    if M = checksign(pseudoE, sM) then
    out(mix, (M, sM, pseudoE, exam, pseudoC));


    out(bb, alpha)
    .

(*-----------------Question Committee-----------------*)
let QC (skQC : skey) =
    in(ch, exam: id);
    new qi: bitstring;
    let Q = (qi, sign(skQC, (qi, exam))) in
    out(ch, (Q, exam)).

(*-----------------Examiner-----------------*)
let Ex (pseudo_skE: skey, CrE: bitstring, idEA: id) =

    let pseudoE = pk(pseudo_skE) in
    (* Registration *)
    in(bb, exam: id);
    out(mix, (CrE, pseudoE, exam));

    in(mix, (=exam, isreg: bool));

    if isreg = true then

    (* Marking *)
    in(bb, cipher: bitstring);
    let (qi: bitstring, si: bitstring, sSEA: bitstring, =exam) = decrypt(pseudo_skE, cipher) in
    if (qi, si) = checksign(idEA, sSEA) then
    new mi: bitstring;
    let M = (qi, si, mi) in
    let sM = sign(pseudo_skE, M) in
    out(mix, (M, sM, pseudoE, CrE, exam))
    .

(*-----------------Candidate-----------------*)
let Cand (pseudo_skC: skey, CrC: bitstring, idEA: id, idQC: id, priv_ch: channel, ansC: bitstring) =

    let pseudoC = pk(pseudo_skC) in
    (* Registration *)
    in(bb, exam: id);
    out(mix, (CrC, pseudoC, exam));

    in(mix, (=exam, isreg: bool));

    if isreg = true then

    (* Testing *)
    in(mix, (test: bitstring, sTest: bitstring));
    if test = checksign(idEA, sTest) then
    let (eQ: bitstring, pseudoC2: id, pseudoE: id) = test in
    if pseudoC2 = pseudoC then
    let (qi: bitstring, sqi: bitstring) = decrypt(pseudo_skC, eQ) in
    if (qi, exam) = checksign(idQC, sqi) then

    (* Testing - Create answer *)
    let si = ansC in
    let S = (qi, si) in
    let sS = sign(pseudo_skC, S) in
    out(mix, (S, sS, pseudoC, CrC, exam));

    in(mix, (rC: bitstring, srC: bitstring));

    (* Marking *)

    in(mix, (M: bitstring, sM: bitstring, =pseudoE, =exam, =pseudoC));
    if M = checksign(pseudoE, sM) then
    let (=qi, =si, mi: bitstring) = M in
    out(priv_ch, (pseudo_skC, qi, si, pseudoC))
    .

(*-----------Test-----------*)
let test (idEA: id, priv_ch: channel) =
    in(priv_ch, (priv_C: skey,ques: bitstring, ans: bitstring, pseudo_C: id));
    in(chan, (pseudo_skE: skey, CrE: bitstring));
    let pseudo_E = pk(pseudo_skE) in
    in(bb, exam: id);
    in(mix, (M: bitstring, sM: bitstring, =pseudo_E, =exam, pseudo_C': id));

    let (ques': bitstring, ans': bitstring, mark: bitstring) = checksign(pseudo_E, sM) in
    event marked(pseudo_C', ques', ans');

    if ques'=ques && ans'=ans && pseudo_C'=pseudo_C then 
    event ok(pseudo_C, ques, ans) else event bad
    .

process

!(
    (*Write only channels for the attacker*)
     !in(tmpC, x:bitstring); out(mix, x);
     !in(tmpC, x:bitstring); out(ch, x);

    (*There is an Attribute-based Credential Authority*)
    new skABCA: skey; let idABCA = pk(skABCA) in out (chan, idABCA);

    (*We assume one Exam Authority and one Question Committee*)
    new skEA: skey; let idEA = pk(skEA) in out (chan, (skEA, idEA));
    new skQC: skey; let idQC = pk(skQC) in out (chan, idQC);

    new pch: channel;

    (!( 
        EA(skEA, idABCA, idQC)
    )) |
    (!(
        QC(skQC)
    ))  |
    (!(
        new pseudo_skE: skey;
        new credE: bitstring;
        let scredE = sign(skABCA, (credE, E)) in
        let CrE = (credE, scredE) in
        new mark:bitstring;
        out(chan, (pseudo_skE, CrE))
    )) |
    (
        new pseudo_skC: skey;
        new credC: bitstring;
        let scredC = sign(skABCA, (credC, C)) in
        let CrC = (credC, scredC) in
        new ansC: bitstring;
        Cand(pseudo_skC, CrC, idEA, idQC, pch, ansC)
    ) |
    (!( 
        new pseudo_skC: skey;
        new credC: bitstring;
        let scredC = sign(skABCA, (credC, C)) in
        let CrC = (credC, scredC) in
        new ans:bitstring;
        out(tmpC, (pseudo_skC, CrC))
    )) |
    (!(test(idEA, pch)))
)